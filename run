#!/usr/bin/env perl

use Mojo::Base -strict;

use lib "../cheatsheet/lib";

use Const::Fast;
use Getopt::Long;
use Term::ANSIColor qw(:constants);
use Text::ASCIITable;

use Advent::Utils::Problem qw(run_part submit_part);

GetOptions(
    "all|a"      => \my $all,
    "day|d=i"    => \my $day,
    "notime|t"   => \my $notime,
    "nosubmit|s" => \my $nosubmit,
    "year|y=i"   => \my $year,
);

const my $TIMEOUT => 60;

my ($mday, $month, $cur_year) = (localtime(time))[3..5];

# Set month and day to sensible values
$month++;
$cur_year += 1900;

if (!defined $day && !defined $all) {
    warn "No day provided. Trying to use current day.\n";

    if ($month != 12) {
        warn "Not in December. Aborting.\n";
        exit;
    }

    if ($mday > 25) {
        warn "Not in Advent. Aborting.\n";
        exit;
    }

    $day = $mday;
}

if (!defined $year) {
    warn "No year provided. Using current year.\n";
    $year = $cur_year;
}

if (defined $all) {
    my $end = 25;
    if (defined $day && $day < 25) {
        $end = $day;
    } elsif ($month eq 12) {
        $end = $mday;
    }

    my $runtimes = {};

    for (1..$end) {
        say BLUE "Running $year day $_:";
        my ($o, $t) = run_part($year, $_, 1, $TIMEOUT);
        if ($_ == 25) {
            $runtimes->{$_} = { t1 => $t };
            last;
        }
        my ($o2, $t2) = run_part($year, $_, 2, $TIMEOUT);
        print "\n";

        $runtimes->{$_} = {
            t1 => $t,
            t2 => $t2,
        };
    }

    my $t = Text::ASCIITable->new({ headingText => 'Runtimes' });
    $t->setCols('Day', 'Part 1 Runtime', 'Part 2 Runtime');
    for my $d (sort { $a <=> $b } keys %$runtimes) {
        my $t1 = $runtimes->{$d}->{t1};
        $t1 = $t1 == -1 ? "> $TIMEOUT secs" : $t1 . ' ms';
        my $t2 = '-';
        if ($d < 25) {
            $t2 = $runtimes->{$d}->{t2};
            $t2 = $t2 == -1 ? "> $TIMEOUT secs" : $t2 . ' ms';
        }

        $t->addRow($d, $t1, $t2);
    }

    print "\n";
    say $t;
} else {
    # TODO: automatically work out which part to run in the default case?
    say BLUE "Running $year day $day:";
    for (1..2) {
        my $answer = run_part($year, $day, $_, $TIMEOUT);
        chomp $answer;

        if ($answer eq '') {
            say RED "No answer outputted. Skipping part $_.";
            next;
        }

        my $to_submit = (split "\n", $answer)[-1];

        submit_part($year, $day, $_, $to_submit) unless $nosubmit;
    }
}

